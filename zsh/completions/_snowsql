#compdef snowsql

_arguments -s -S \
  '(-a --accountname)'{-a,--accountname}'[Snowflake account identifier]:account:' \
  '(-u --username)'{-u,--username}'[Username to connect with]:user:' \
  '(-d --dbname)'{-d,--dbname}'[Default database name]:database:' \
  '(-s --schemaname)'{-s,--schemaname}'[Default schema name]:schema:' \
  '(-r --rolename)'{-r,--rolename}'[Role to assume]:role:' \
  '(-w --warehouse)'{-w,--warehouse}'[Virtual warehouse to use]:warehouse:' \
  '(-h --host)'{-h,--host}'[Custom host endpoint]:host:' \
  '(-p --port)'{-p,--port}'[Port to connect to]:port number:' \
  '--region[Legacy region suffix (account.region)]:region:' \
  '(-m --mfa-passcode)'{-m,--mfa-passcode}'[MFA token to append]:passcode:' \
  '--mfa-passcode-in-password[Append MFA token directly to password]' \
  '--abort-detached-query[Cancel detached query when connection drops]' \
  '--probe-connection[Test TLS/SSL connectivity]' \
  '--proxy-host[Proxy hostname]:host:' \
  '--proxy-port[Proxy port]:port number:' \
  '--proxy-user[Proxy username]:user:' \
  '--authenticator[Authentication method]:method:(snowflake externalbrowser oauth)' \
  '(-v --version)'{-v,--version}'[Show or pin SnowSQL version]::version:' \
  '--noup[Disable auto-upgrade for this invocation]' \
  '(-D --variable)'{-D,--variable}'[Define SQL variable]:name=value:' \
  '(-o --option)'{-o,--option}'[Set SnowSQL config option]:name=value:' \
  '(-f --filename)'{-f,--filename}'[Run SQL statements from file]:file:_files' \
  '(-q --query)'{-q,--query}'[Execute inline SQL]:sql:' \
  '--config[Alternate snowsql config file]:file:_files' \
  '(-P --prompt)'{-P,--prompt}'[Force password prompt]' \
  '(-M --mfa-prompt)'{-M,--mfa-prompt}'[Force MFA prompt]' \
  '(-c --connection)'{-c,--connection}'[Named connection profile]:profile:' \
  '--single-transaction[Wrap statements in BEGIN/COMMIT]' \
  '--private-key-path[PEM key for key-pair auth]:file:_files' \
  '(-U --upgrade)'{-U,--upgrade}'[Force SnowSQL self-upgrade]' \
  '(-K --client-session-keep-alive)'{-K,--client-session-keep-alive}'[Keep session alive indefinitely]' \
  '--disable-request-pooling[Disable request pooling (faster failover)]' \
  '--token[OAuth access token to reuse]:token:' \
  '--query_tag[Apply tag to issued queries]:tag:' \
  '--generate-jwt[Emit signed JWT instead of running session]' \
  '--oauth-client-id[Identity provider client id]:client id:' \
  '--oauth-redirect-uri[Redirect URI registered with IdP]:uri:' \
  '--oauth-authorization-url[IdP authorization endpoint]:url:' \
  '--oauth-token-request-url[IdP token endpoint]:url:' \
  '--oauth-scope[OAuth scope request]:scope:' \
  '--oauth-disable-pkce[Disable PKCE flow]' \
  '--oauth-enable-refresh-tokens[Opt-in to refresh tokens]' \
  '--oauth-enable-single-use-refresh-tokens[Single-use refresh tokens]' \
  '--workload-identity-provider[Select workload identity provider]:provider:(AWS AZURE GCP OIDC)' \
  '(-\? --help)'{-\?,--help}'[Show help message]' \
  '*:: :->args'

case $state in
  args)
    _files
  ;;
esac
